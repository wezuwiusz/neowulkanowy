apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play'
apply from: 'jacoco.gradle'
apply from: 'sonarqube.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    playAccountConfigs {
        defaultAccountConfig {
            serviceAccountEmail = System.getenv("PLAY_SERVICE_ACCOUNT_EMAIL")
            pk12File = file('key.p12')
        }
    }

    defaultConfig {
        applicationId "io.github.wulkanowy"
        testApplicationId "io.github.tests.wulkanowy"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 19
        versionName "0.6.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        playAccountConfig = playAccountConfigs.defaultAccountConfig
        manifestPlaceholders = [crashlytics_enabled: project.hasProperty("enableCrashlytics")]
    }

    signingConfigs {
        release {
            storeFile file("upload-key.jks")
            storePassword System.getenv("PLAY_STORE_PASSWORD")
            keyAlias System.getenv("PLAY_KEY_ALIAS")
            keyPassword System.getenv("PLAY_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            buildConfigField "boolean", "CRASHLYTICS_ENABLED", "true"
            minifyEnabled true
            useProguard false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField "boolean", "CRASHLYTICS_ENABLED", project.hasProperty("enableCrashlytics") ? "true" : "false"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            testCoverageEnabled = true
            ext.enableCrashlytics = project.hasProperty("enableCrashlytics")
            multiDexKeepProguard file('proguard-multidex-rules.pro')
        }
    }

    lintOptions {
        disable 'HardwareIds'
    }
}

androidExtensions {
    experimental = true
}

play {
    track = 'alpha'
    uploadImages = true
}

configurations.all {
    resolutionStrategy.force "com.squareup.okhttp3:okhttp-urlconnection:3.11.0"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation('io.github.wulkanowy:api:0.6.0') { exclude module: "threetenbp" }

    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "com.google.android.material:material:1.0.0"
    implementation 'androidx.multidex:multidex:2.0.0'

    implementation 'com.takisoft.preferencex:preferencex:1.0.0'
    implementation "com.mikepenz:aboutlibraries:6.2.0"
    implementation "com.firebase:firebase-jobdispatcher:0.8.5"

    implementation "com.google.dagger:dagger-android-support:2.19"
    kapt "com.google.dagger:dagger-compiler:2.19"
    kapt "com.google.dagger:dagger-android-processor:2.19"

    implementation "androidx.room:room-runtime:2.1.0-alpha03"
    implementation "androidx.room:room-rxjava2:2.1.0-alpha03"
    kapt "androidx.room:room-compiler:2.1.0-alpha03"

    implementation "eu.davidea:flexible-adapter:5.1.0"
    implementation "eu.davidea:flexible-adapter-ui:1.0.0"

    implementation "com.aurelhubert:ahbottomnavigation:2.2.0"
    implementation 'com.ncapdevi:frag-nav:3.0.0'

    implementation 'com.github.pwittchen:reactivenetwork-rx2:3.0.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation "io.reactivex.rxjava2:rxjava:2.2.4"

    implementation "com.jakewharton.threetenabp:threetenabp:1.1.0"

    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "at.favre.lib:slf4j-timber:1.0.1"

    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.7'

    debugImplementation "com.amitshekhar.android:debug-db:1.0.4"

    testImplementation "junit:junit:4.12"
    testImplementation "io.mockk:mockk:1.8.13.kotlin13"
    testImplementation "org.mockito:mockito-inline:2.23.4"
    testImplementation 'org.threeten:threetenbp:1.3.8'

    androidTestImplementation 'androidx.test:core:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'org.mockito:mockito-android:2.23.4'
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'
