apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play'
apply from: 'jacoco.gradle'
apply from: 'sonarqube.gradle'
apply from: 'hooks.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "io.github.wulkanowy"
        testApplicationId "io.github.tests.wulkanowy"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 42
        versionName "0.9.4"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        manifestPlaceholders = [
                fabric_api_key     : System.getenv("FABRIC_API_KEY") ?: "null",
                crashlytics_enabled: project.hasProperty("enableCrashlytics")
        ]
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    signingConfigs {
        release {
            storeFile file("upload-key.jks")
            storePassword System.getenv("PLAY_STORE_PASSWORD")
            keyAlias System.getenv("PLAY_KEY_ALIAS")
            keyPassword System.getenv("PLAY_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            buildConfigField "boolean", "CRASHLYTICS_ENABLED", "true"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField "boolean", "CRASHLYTICS_ENABLED", project.hasProperty("enableCrashlytics") ? "true" : "false"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            testCoverageEnabled = true
            ext.enableCrashlytics = project.hasProperty("enableCrashlytics")
            multiDexKeepProguard file('proguard-multidex-rules.pro')
        }
    }

    flavorDimensions "platform"

    productFlavors {
        play {
            dimension "platform"
        }

        fdroid {
            buildConfigField "boolean", "CRASHLYTICS_ENABLED", "false"
            dimension "platform"
        }
    }

    lintOptions {
        disable 'HardwareIds'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/library_release.kotlin_module'
        exclude 'META-INF/library-core_release.kotlin_module'
    }
}

androidExtensions {
    experimental = true
}

play {
    serviceAccountEmail = System.getenv("PLAY_SERVICE_ACCOUNT_EMAIL") ?: "jan@fakelog.cf"
    serviceAccountCredentials = file('key.p12')
    defaultToAppBundles = false
    track = 'alpha'
}

ext {
    work_manager = "2.2.0"
    room = "2.1.0"
    dagger = "2.24"
    chucker = "2.0.4"
    mockk = "1.9.2"
    mockito_core = "3.0.6"
}

configurations.all {
    resolutionStrategy.force "androidx.constraintlayout:constraintlayout:1.1.3"
    resolutionStrategy.force "com.google.android.material:material:1.1.0-alpha07"
}

dependencies {
    implementation "io.github.wulkanowy:api:0.9.4"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "androidx.core:core:1.0.2"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.fragment:fragment:1.0.0"
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "androidx.multidex:multidex:2.0.1"

    implementation "androidx.preference:preference:1.1.0-rc01"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.viewpager:viewpager:1.0.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.0.0"
    implementation "com.google.android.material:material:1.1.0-alpha07"
    implementation "com.github.wulkanowy:material-chips-input:2.0.1"
    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"
    implementation "me.zhanghai.android.materialprogressbar:library:1.6.1"

    implementation "androidx.work:work-runtime:$work_manager"
    implementation "androidx.work:work-rxjava2:$work_manager"

    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-rxjava2:$room"
    kapt "androidx.room:room-compiler:$room"

    implementation "com.google.dagger:dagger-android-support:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    kapt "com.google.dagger:dagger-android-processor:$dagger"
    implementation "com.squareup.inject:assisted-inject-annotations-dagger2:0.5.0"
    kapt "com.squareup.inject:assisted-inject-processor-dagger2:0.5.0"

    implementation "eu.davidea:flexible-adapter:5.1.0"
    implementation "eu.davidea:flexible-adapter-ui:1.0.0"
    implementation "com.aurelhubert:ahbottomnavigation:2.3.4"
    implementation "com.ncapdevi:frag-nav:3.3.0"

    implementation "com.github.pwittchen:reactivenetwork-rx2:3.0.6"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxjava:2.2.12"

    implementation "com.google.code.gson:gson:2.8.5"
    implementation "com.jakewharton.threetenabp:threetenabp:1.2.1"
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "at.favre.lib:slf4j-timber:1.0.1"
    implementation "com.squareup.okhttp3:logging-interceptor:3.12.3"
    implementation "com.mikepenz:aboutlibraries:7.0.2"

    playImplementation "com.google.firebase:firebase-core:17.2.0"
    playImplementation "com.crashlytics.sdk.android:crashlytics:2.10.1"

    releaseImplementation "fr.o80.chucker:library-no-op:$chucker"

    debugImplementation "fr.o80.chucker:library:$chucker"
    debugImplementation "com.amitshekhar.android:debug-db:1.0.6"

    testImplementation "junit:junit:4.12"
    testImplementation "io.mockk:mockk:$mockk"
    testImplementation "org.threeten:threetenbp:1.4.0"
    testImplementation "org.mockito:mockito-core:$mockito_core"
    testImplementation("org.mockito:mockito-inline:3.0.6") {
        exclude group: "org.mockito", module: "mockito-core"
    }

    androidTestImplementation "androidx.test:core:1.2.0"
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "io.mockk:mockk-android:$mockk"
    androidTestImplementation "androidx.room:room-testing:$room"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    androidTestImplementation "org.mockito:mockito-core:$mockito_core"
    androidTestImplementation("org.mockito:mockito-android:3.0.6") {
        exclude group: 'org.mockito', module: 'mockito-core'
    }
}

apply plugin: 'com.google.gms.google-services'
