apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'// sync warning probably caused by bug https://issuetracker.google.com/issues/74537216
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply from: 'jacoco.gradle'
apply from: 'android-sonarqube.gradle'
apply plugin: 'com.google.gms.oss.licenses.plugin'
apply plugin: 'com.github.triplet.play'

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://maven.fabric.io/public' }
        google()
    }

    dependencies {
        classpath "io.fabric.tools:gradle:$fabricGradle"
        classpath "com.google.gms:oss-licenses:0.9.2"
        classpath "com.github.triplet.gradle:play-publisher:$playPublisher"
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    playAccountConfigs {
        defaultAccountConfig {
            serviceAccountEmail = System.getenv("PLAY_SERVICE_ACCOUNT_EMAIL")
            pk12File = file('key.p12')
        }
    }

    defaultConfig {
        applicationId "io.github.wulkanowy"
        testApplicationId "io.github.tests.wulkanowy"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 16
        versionName "0.5.2"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        playAccountConfig = playAccountConfigs.defaultAccountConfig
        manifestPlaceholders = [
                fabricApiKey: System.getenv("FABRIC_API_KEY") ?: "null"
        ]
    }

    signingConfigs {
        release {
            storeFile file("upload-key.jks")
            storePassword System.getenv("PLAY_STORE_PASSWORD")
            keyAlias System.getenv("PLAY_KEY_ALIAS")
            keyPassword System.getenv("PLAY_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            testCoverageEnabled = true
            ext.enableCrashlytics = false
            multiDexKeepProguard file('proguard-multidex-rules.pro')
        }
    }

    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}

androidExtensions {
    experimental = true
}

play {
    track = 'alpha'
    uploadImages = true
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:$supportVersion"
}

dependencies {
    implementation project(':api')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:preference-v14:$supportVersion"
    implementation "com.firebase:firebase-jobdispatcher:$firebaseJob"
    implementation "org.apache.commons:commons-lang3:$apacheLang"
    implementation "org.apache.commons:commons-collections4:$apacheCollections"
    implementation "eu.davidea:flexible-adapter:$flexibleAdapter"
    implementation "eu.davidea:flexible-adapter-ui:$flexibleUi"
    implementation "com.google.dagger:dagger-android-support:$dagger2"
    implementation "com.aurelhubert:ahbottomnavigation:$ahbottom"
    implementation "com.jakewharton.threetenabp:threetenabp:$threeTenABP"
    implementation "com.google.android.gms:play-services-oss-licenses:$ossLicenses"
    implementation "com.jakewharton.timber:timber:$timber"
    implementation "at.favre.lib:slf4j-timber:$slf4jTimber"
    implementation "android.arch.persistence.room:runtime:1.1.1"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation "io.reactivex.rxjava2:rxjava:2.2.0"
    implementation 'com.android.support:multidex:1.0.3'
    implementation "android.arch.persistence.room:rxjava2:1.1.1"
    implementation 'com.github.pwittchen:reactivenetwork-rx2:2.1.0'
    implementation 'com.ncapdevi:frag-nav:3.0.0-RC3'

    implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsSdk@aar") {
        transitive = true
    }
    implementation("com.crashlytics.sdk.android:answers:$crashlyticsAnswers@aar") {
        transitive = true
    }

    kapt "com.google.dagger:dagger-compiler:$dagger2"
    kapt "com.google.dagger:dagger-android-processor:$dagger2"
    kapt "android.arch.persistence.room:compiler:1.1.1"

    debugImplementation "com.amitshekhar.android:debug-db:$debugDb"

    testImplementation "junit:junit:$junit"
    testImplementation "org.mockito:mockito-inline:$mockito"
    testImplementation "org.jsoup:jsoup:$jsoup"

    androidTestImplementation "com.android.support.test:runner:$testRunner"
    androidTestImplementation "org.mockito:mockito-android:$mockito"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}
